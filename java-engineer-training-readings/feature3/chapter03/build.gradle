apply plugin: 'java'
apply plugin: 'idea'

ext.jdkVersion = '1.8'
sourceCompatibility = jdkVersion
tasks.withType(AbstractCompile) each {
    it.options.encoding = 'UTF-8'
}

configurations {
    provided
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    provided 'org.projectlombok:lombok:1.14.8'
    testCompile "junit:junit:4.11"
}

task initDirs {
    description = 'Initialize `mavenized` directory structure.'
    doLast {
        ['src/main/java', 'src/main/resources', 'src/test/java', 'src/test/resources'].each {
            def path = "${projectDir}/${it}"
            ant.mkdir dir: path
            ant.touch file: "${path}/.gitkeep"
        }
    }
}

idea {
    project {
        jdkName = jdkVersion
        languageLevel = jdkVersion
    }
    module {
        scopes.PROVIDED.plus += [configurations.provided]
        downloadJavadoc = true
        downloadSources = true
    }
}

